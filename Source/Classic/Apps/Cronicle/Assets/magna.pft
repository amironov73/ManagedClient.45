'{'

/* Первый автор

(if p(v961) then /* Индивидуальный автор ZPEC
  if v961^z:'да' then
    v961^a, ", "v961^b,
    ' ',
  fi,
fi),

(if p(v962) then /* Коллектив SPEC
  if v962^a:'да' then
    v962^a, /* Наименование
    " ("v962^c")", /* Город
  fi,
fi),

if p(v700) then /* Индивидуальный автор PAZK
  v700^a, ", "v700^b,
  ' ',
fi,


if p(v710) then /* Коллектив PAZK
  v710^a, /* Наименование
  " ("v710^c")", /* Город
  ' ',
fi,

/* Заглавие и сведения об ответственности SPEC

if p(v461) then
  (if p(v461) then
    v461^c, /* Заглавие    
    if v900^t <> 'a' then ' : [', &uf('Koomr.mnu\', v900^t) ']', fi, /* Вид документа
    " : "v461^e, /* Сведения, относящиеся к заглавию
    " / "v461^f, /* Cведения об ответственности
    ". - "v461^p, /* Сведения об издании
    ". - "v461^d, /* Город
    " : "v461^g, /* Издательство
    ", "v461^h, /* Год начала издания
    if v461^h <> v461^z then ' - ', v461^z fi, /* Год окончания
  fi),
  '.\par ',
fi,

/* Заглавие и сведения об ответственности PAZK

if p(v200^v) then /* Номер тома
  v200^v,
  if p(v200^a) then ' : ', fi,
fi,


(if p(v510^d) then /* Параллельные заглавия
  " = "v510^d,
  " : "v510^e, " / "v510^f, " ; "v510^g,
fi),

v200^a,

if a(v461) then
  if p(v200^b) then
    if v200^b : 'текст' then else v200^b, fi,
  else
    if v900^t <> 'a' then ' : [', &uf('Koomr.mnu\', v900^t), ']', fi, /* Вид документа
  fi,
fi,

" : "v200^e, /* Сведения, относящиеся к заглавию
" / "v200^f, /* Первые сведения об ответственности
" ; "v200^g, /* Последующие сведения об ответственности

" : ["v900^z"]", /* возрастные ограничения

(if p(v923) then /* Выпуск, часть
  ' ',
  v923^h, /* Обозначение и номер второй единицы деления
  if p(v923^h) then '. ' fi,
  v923^i, /* Заглавие второй единицы деления
  if s(v923^k,v923^l) <> '' then ' ' fi, /* Есть третья единица?
  v923^k, /* Обозначение и номер третьей единицы деления
  if p(v923^k) then '. ' fi,
  v923^l, /* Заглавие треьей единицы деления
fi),

/* Издание

if p(v205) then
  '. - ',
  v205^a,
fi,

/* Специфические сведения

/* Выходные данные

if p(v210) then
  '. - ',
  ( 
    v210^a, /* город1
    " ; "v210^x, /* город2
    " ; "v210^y, /* город3
    " : "v210^c, /* издательство
    if s(v210^a,v210^x,v210^y,v210^c) <> '' then ', ', fi, v210^d, /* год
  ),
fi,

/* Физическая характеристика

if p(v215^a) then
  '. - ',
  v215^a, /* объем
  if p(v215^1) then " "v215^1 else ' c.' fi, /* единица измерения
  if v215^c*1.1='-' then " : "v215^c*2, else " : "v215^c, fi, /* иллюстрации
  if v215^0*1.1='-' then ", "v215^0*2, else ", "v215^0, fi, /* иллюстрации
  if v215^7*1.1='-' then ", "v215^7*2, else ", "v215^7, fi, /* иллюстрации
  if v215^8*1.1='-' then ", "v215^8*2, else ", "v215^8, fi, /* иллюстрации
fi,

/* Серия

if p(v225^v) then '. - ', fi,

(if p(v225^v) then
  '(',
  v225^a,
  " : "v225^v,
  ')',
fi),

/* Разночтения заглавий

(if p(v517^a) then
  '. - ',
  v517^t*4": ", /* Тип разночтения
  v517^a,
fi),

/* Тираж

if p(v215^x) then
  if v215^x:'б. т.' then else
    '. - ',
    v215^x" экз.",
  fi,
fi,

/* Примечания

(if p(v912) then 
  '. - ', v912, /* Примечания о языке
fi),

(if p(v314) then
  '. - ', v314, /* Примечания об интеллектуально ответственности
fi),


(if p(v320) then
  '. - ', v320, /* Примечания о библиографии
fi),

/* Стандартный номер

if p(v10^a) then
  '. - ISBN ',
  &uf('Av10^a#1'),
fi,

&uf('!'), /* Очистка от двойных точек
'}'
