'{',

/* Скрипт для Книжной летописи
/* g1 - временная переменная
/* g2 - признак того, что текст жирным шрифтом уже выведен
/* g3 - признак того, что был установлен абзацный отступ

&uf('+7'),

/* Отступ первого абзаца
'\fi300 ',

/* Первый автор

(if p(v961) then /* Индивидуальный автор SPEC
  if v961^z:'да' then
    '{\b ',
    &uf('+S1',v961^a),
    ", "v961^b,
    '}\~',
    &uf('+7W2#1'),
  fi,
fi),

if a(v961) then
(if p(v962) then /* Коллектив SPEC
  if v962^z:'да' then
    '{\b ',
    &uf('+S1',v962^a), /* Наименование
    "\~("v962^c")", /* Город
    '}\~',
    &uf('+7W2#1'),
  fi,
fi),
fi,

if p(v700) then /* Индивидуальный автор PAZK
  '{\b ', 
  &uf('+S1',v700^a),
  ", "v700^b,
  '}\~',
  &uf('+7W2#1'),
fi,

if p(v710) then /* Коллектив PAZK
  '{\b ',
  &uf('+S1',v710^a), /* Наименование
  " ("v710^c")",  /* Город
  '}\~',
  &uf('+7W2#1'),
fi,

/* Заглавие и сведения об ответственности SPEC

if p(v461) then
  (if p(v461) then    
  
    /* Заглавие
    if p(g2) then
      &uf('+S1',v461^c),
    else
      '{\b ', &uf('+S1',v461^c), '}',
      &uf('+7W2#1'),
    fi,
  fi),

  /* Вид документа пропускаем
  
  |\~: |v461^e,     /* Сведения, относящиеся к заглавию
  "\~: ["v900^z"]", /* возрастные ограничения

  "\~/\~"|\~; |+v461^f, /* Cведения об ответственности
  
  ".\~\endash\~"v461^p, /* Сведения об издании
  ".\~\endash\~"v461^d, /* Город
  |\~: |v461^g, /* Издательство
  |, |v461^h, /* Год начала издания
  if v461^h <> v461^z then ' - ', v461^z fi, /* Год окончания

  '.\par\fi300 ',
fi,

/* Заглавие и сведения об ответственности PAZK

if p(v200^v) then /* Номер тома
   '{\b ', v200^v, '}',
   &uf('+7W2#1'),
  if p(v200^a) then ' : ', fi,
fi,

if p(g2) then
  ' ',
  &uf('+S1',v200^a),
else
  '{\b ', &uf('+S1',v200^a), '}',
fi,

(if p(v510^d) then /* Параллельные заглавия
  "\~=\~"v510^d,
  "\~:\~"v510^e, " / "v510^f, " ; "v510^g,
fi),

/* Вид документа пропускаем

"\~: "v200^e, /* Сведения, относящиеся к заглавию
if a(v461) then
  "\~: ["v900^z"]", /* возрастные ограничения
fi
"\~/ "v200^f, /* Первые сведения об ответственности
"\~; "v200^g, /* Последующие сведения об ответственности

(if p(v923) then /* Выпуск, часть
  ' ',
  v923^h, /* Обозначение и номер второй единицы деления
  if p(v923^h) then '. ' fi,
  v923^i, /* Заглавие второй единицы деления
  if s(v923^k,v923^l) <> '' then ' ' fi, /* Есть третья единица?
  v923^k, /* Обозначение и номер третьей единицы деления
  if p(v923^k) then '. ' fi,
  v923^l, /* Заглавие треьей единицы деления
fi),

/* Издание

if p(v205) then
  '.\~\endash\~',
  v205^a,
fi,

/* Специфические сведения

if s(v123^d) <> '' then /* Карты
  '.\~\endash\~',
  '1:', v123^d, /* Масштаб
fi,

if s(v125^f) <> '' then /* Ноты
  '.\~\endash\~',
  v125^f, /* Форма изложения
fi,

/* Выходные данные

if p(v210) then
  '.\~\endash\~',
  ( 
    v210^a, /* город1
    if a(v461) then "[Б. м.]"n210^a, fi, /* город1
    "\~; "v210^x, /* город2
    "\~; "v210^y, /* город3
    "\~: "v210^c, /* издательство
    if p(v461) then v210^d, else ", "v210^d, fi, /* год
    "\~("v210^1")", /* место печати
  ),
fi,

/* Физическая характеристика

if p(v215^a) then
  '.\~\endash\~',
  v215^a, /* объем
  if p(v215^1) then '\~', &unifor('G2='v215^1), else '\~c.' fi, /* единица измерения
  if v215^c*1.1='-' then " : "v215^c*2, else "\~: "v215^c, fi, /* иллюстрации
  if v215^0*1.1='-' then ", "v215^0*2, else ", "v215^0, fi, /* иллюстрации
  if v215^7*1.1='-' then ", "v215^7*2, else ", "v215^7, fi, /* иллюстрации
  if v215^8*1.1='-' then ", "v215^8*2, else ", "v215^8, fi, /* иллюстрации
  "\~; "v215^d, /* размер в сантиметрах
fi,

/* Серия

if p(v225^a) then '.\~\endash\~', fi,

(if p(v225^a) then
  '(',
  v225^a,
  "\~: "v225^v,
  ')',
fi),

/* Разночтения заглавий

(if p(v517^a) then
  if v517^t: 'составленное' then else
    if a(v517^u) then
      '.\~\endash\~',
      if p(v517^t) then v517^t*4, ':\~', fi,
      v517^a,
    fi,
  fi,
fi),

/* Примечания

(if p(v912) then 
  '.\~\endash\~', v912, /* Примечания о языке
fi),

(if p(v314) then
  '.\~\endash\~', v314, /* Примечания об интеллектуально ответственности
fi),


(if p(v320) then
  '.\~\endash\~', v320, /* Примечания о библиографии
fi),

/* Тираж

if p(v215^x) then
  if v215^x:'б. т.' then else
    '.\~\endash\~',
    v215^x"\~экз.",
  fi,
fi,

/* Перевод заглавия

(if p(v541) then 
  '.\~\endash\~Перевод заглавия:\~', v541,
fi),

/* Стандартный номер

if p(v10^a) then
  '.\~\endash\~ISBN\~',
  &uf('Av10^a#1'),
  if p(v215^3) then /* вид упаковки
    ' (', &uf('Av215^3#1'), ')',
  else
    ' (в пер.)',
  fi,
  '.',
fi,

if p(v203) then
  if s(v203^a,':',v203^c) <> 'Текст:непосредственный' then,
    ( 
      if p(v203^a) then
        '.\~\endash\~', v203^a, " ("v203^o")", " : "v203^c, '.',
      fi
    )
  fi,
fi,

'.', /* Завершающая точка

if s(v621,v675) <> '' then /* УДК и ББК
  '\par\qr ',
  "УДК "v675^*+|+|"\line ",  
  "ББК "v621^*+|+|,
  '\par',
else
  '\par',
fi,

&uf('!'), /* Очистка от двойных точек
'}'
